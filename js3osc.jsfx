desc:JS3Osc
tags: synth, instrument

slider1:maxpoly=16<2, 32, 1>-Polyphony

slider2:osc1_vol=-12<-60, 0, .01>Osc1 Volume
slider3:osc1_wave=0<0, 5, 1{Sine,Triangle,Saw,Square,Pulse,Noise}>Osc1 Wave
slider4:osc1_phase_offset=0<-1,1,0.01>Osc1 Phase Offset
slider5:osc1_phase_det=0<-100, 100, 1>Osc1 Phase Detune
slider6:osc1_coarse=0<-24, 24, 1>Osc1 Coarse
slider7:osc1_fine=0<-99, 99, 1>Osc1 Fine
slider8:osc1_pan=0<-1, 1, 0.01>Osc1 Pan
slider9:osc1_inv=0<0, 1, 1{No, Yes}>Osc1 Invert phase

slider30:env_att=1<1, 10000, 1:log>Attack
slider31:env_dec=1<1, 10000, 1:log>Decay
slider32:env_sus=0<-90, 0, .1:log>Sustain Db
slider33:env_rel=500<1, 10000, 1:log>Release

slider35:pitch_amt=0<-24,24,0.1>Pitch amount
slider36:pitch_att=1<1, 10000, 1:log>Pitch Attack
slider37:pitch_dec=1<1, 10000, 1:log>Pitch Decay
slider38:pitch_sus=1<0, 1, 0.01>Pitch Sustain
slider39:pitch_rel=500<1, 10000, 1:log>Pitch Release

slider40:filter_shape=0<0,2,1{Off,Low Pass,Band Pass,High Pass}>Filter shape
slider41:filter_freq=11000<20, 22000, 1:log>Filter frequency
slider42:filter_q=0.70<0.01, 40, 0.01:log>Filter Q
slider43:filter_adsr_amt=0<-1, 1, 0.01>Filter ASDR amt.
slider44:filter_att=1<1, 10000, 1:log>Filter Attack
slider45:filter_dec=1<1, 10000, 1:log>Filter Decay
slider46:filter_sus=1<0, 1, 0.01>Filter Sustain
slider47:filter_rel=500<1, 10000, 1:log>filter Release

import 3o.adsr.jsfx-inc
import 3o.array.jsfx-inc
import 3o.zdf_filter.jsfx-inc

options:gfx_hz=60

@init

poly.array_init(0, 128, 5); // [note, velocity, freq, phase-left, phase-right]
envelope = 2000; // 128 * 7 buffer for keys adsr
remove_ptrs.array_init(4000, 128, 1);
pitch_env = 5000; // 128 * 7 buffer for pitch adsr
filter_arr_l = 6000; // 128 * 11 buffer for filters one per key left channel
filter_arr_r = 8000; // 128 * 11 buffer for filters one per key right channel
filter_env = 10000; // 128 * 7 buffer for filter adsr

function wrap(number) (number <= 1 ? number : number - 1;);
function sine_wave(cycle) (sin(cycle * 2 * $pi););
function tri_wave(cycle) (
  cycle < 0 ? (
    4 * cycle + 2 - 1;
  ) : (
    cycle > 0.5
      ? 4 * cycle - 2 - 1
      : -4 * cycle + 2 - 1;
  );
);
function saw_wave(cycle) (
  cycle < 0
  ? cycle * -2 - 1
  : cycle * -2 + 1;
);
function square_wave(cycle) (
  cycle < 0 ? (
    cycle < -0.5 ? -1 : 1;
  ) : (
    cycle < 0.5 ? -1 : 1;
  );
);
function pulse_wave(cycle) (
  cycle < 0 ? (
    cycle < -0.25 ? -1 : 1;
  ) : (
    cycle < 0.25 ? -1 : 1;
  );
);
function make_noise() (rand(2) - 1;);
function db2gain(db) (10^(db / 20););
function round(in) (floor(in + 0.5 * sign(in)););
function note2freq(n) (440 * pow(2, (n - 69) / 12););
function freq2note(f) ( round(12*(log(f/440)/log(2))+69); );

function make_wave(wave, cycle) (
  wave == 0 ? sine_wave(cycle)
    : wave == 1 ? tri_wave(cycle)
    : wave == 2 ? saw_wave(cycle)
    : wave == 3 ? square_wave(cycle)
    : wave == 4 ? pulse_wave(cycle)
    : make_noise();
);

// copy filter coeficients from buffer1 to buffer2
function filter_copy_coefs(buf1, buf2) (
  buf2[2] = buf1[2];
  buf2[3] = buf1[3];
  buf2[4] = buf1[4];
);

// wraps filter function using buffers
function filter_setf(buf, freq, q) (
  filter.zdf_setf(freq, q);
  buf[2] = filter.g;
  buf[3] = filter.r2;
  buf[4] = filter.h;
);

// wraps filter function using buffers
function filter_lp(buf, sample) (
  filter.s1 = buf[0];
  filter.s2 = buf[1];
  filter.g = buf[2];
  filter.r2 = buf[3];
  filter.h = buf[4];
  filter.zdf_svf_lp(sample);
  buf[0] = filter.s1;
  buf[1] = filter.s2;
);

@block

while (midirecv(offset, msg1, note, vel)) (
  event = msg1 & 0xF0;
  channel = msg1 & 0x0F;

  // Note on
  event == 0x90 && vel ? (

    // remove note if it is already playing
    note_repeat = 0;
    ptr = poly.array_find(note);
    ptr >= 0 ? (
      note_repeat = 1;
      last_phasel = ptr[3];
      last_phaser = ptr[4];
      poly.array_remove(ptr);
    );

    // if polyphony is full and not mono remove first element
    poly.size == maxpoly && maxpoly != 0 ? (
      poly.array_remove(poly.array_first());
    );

    ptr = poly.array_add();
    ptr[0] = note;
    ptr[1] = vel / 127;
    ptr[2] = note2freq(note) / srate;
    ptr[3] = note_repeat ? last_phasel : 0; // osc start phase left
    ptr[4] = note_repeat ? last_phaser : 0; // osc start phase right

    env = envelope + note * 7;
    adsr_seta(env_att * 0.001, env);
    adsr_setd(env_dec * 0.001, env);
    adsr_sets(exp(log(10)/20 * env_sus), env);
    adsr_setr(env_rel * 0.001, env);
    adsr_a(vel / 127, env);

    env = pitch_env + note * 7;
    adsr_seta(pitch_att * 0.001, env);
    adsr_setd(pitch_dec * 0.001, env);
    adsr_sets(pitch_sus, env);
    adsr_setr(pitch_rel * 0.001, env);
    adsr_a(1, env);
    
    env = filter_env + note * 7;
    adsr_seta(filter_att * 0.001, env);
    adsr_setd(filter_dec * 0.001, env);
    adsr_sets(filter_sus, env);
    adsr_setr(filter_rel * 0.001, env);
    adsr_a(1, env);
  );

  // Note off
  event == 0x80 || (event == 0x90 && !vel) ? (
    ptr = poly.array_find(note);
    ptr >= 0 ? (
      adsr_r(envelope + note * 7);
      adsr_r(pitch_env + note * 7);
      adsr_r(filter_env + note * 7);
    );
  );

  // All notes off
  event == 0xB0 && note == 123 ? (
    poly.array_clear();
  );

  midisend(offset, msg1, note, vel);
);

@slider
osc1_gain = db2gain(osc1_vol);
osc1_st_det = pow(2, osc1_phase_det / 1200); // stereo detune
osc1_fine_det = pow(2, osc1_fine / 1200);
osc1_coarse_det = pow(2, osc1_coarse / 12);

@sample

remove_ptrs.array_clear();
ptr = poly.array_first();
while(ptr >= 0) ( // for each note/voice
  out_l = 0;
  out_r = 0;
  envbuf = envelope + ptr[0] * 7;
  env_state = adsr_process(envbuf);
  pitchbuf = pitch_env + ptr[0] * 7;
  adsr_process(pitchbuf);
  osc_pitch_det = pow(2, pitchbuf[0] * pitch_amt / 12);

  // OSC1
  osc_det = osc1_coarse_det * osc1_fine_det * osc_pitch_det;
  ptr[3] = wrap(ptr[3] + ptr[2] * osc1_st_det * osc_det); // phase left
  ptr[4] = wrap(ptr[4] + ptr[2] / osc1_st_det * osc_det); // phase right
  wave_l = make_wave(osc1_wave, wrap(ptr[3] + osc1_phase_offset * 0.25));
  wave_r = make_wave(osc1_wave, wrap(ptr[4] - osc1_phase_offset * 0.25));
  out_l += wave_l * (1 - osc1_pan) * envbuf[] * osc1_gain;
  out_r += wave_r * (1 + osc1_pan) * envbuf[] * osc1_gain;

  filterbuf = filter_env + ptr[0] * 7; // envelope buffer
  adsr_process(filterbuf);
  filterbuf_l = filter_arr_l + ptr[0] * 11; // filter buffer left
  filterbuf_r = filter_arr_r + ptr[0] * 11; // filter buffer right
  
  multiplier = pow(20000/filter_freq, filterbuf[0] * filter_adsr_amt);
  filter_setf(filterbuf_l, filter_freq * multiplier, filter_q);
  filter_copy_coefs(filterbuf_l, filterbuf_r);
  out_l = filter_lp(filterbuf_l, out_l);
  out_r = filter_lp(filterbuf_r, out_r);
  
  spl0 += out_l;
  spl1 += out_r;
  
  // mark note for deletion
  env_state == 0 ? (
    p = remove_ptrs.array_add();
    p[0] = ptr[0];
  );

  ptr = poly.array_next(ptr);
);

// remove notes that finished adsr
ptr = remove_ptrs.array_first();
while(ptr >= 0) (
  p = poly.array_find(ptr[0]);
  p >= 0 ? poly.array_remove(p);
  ptr = remove_ptrs.array_next(ptr);
);

//freq = 220;
//
//osc_det = osc1_coarse_det * osc1_fine_det;
//cyclel = wrap(cyclel + freq / srate * osc1_st_det * osc_det);
//cycler = wrap(cycler + freq / srate / osc1_st_det * osc_det);
//wave_l = make_wave(osc1_wave, wrap(cyclel + osc1_phase_offset * 0.25));
//wave_r = make_wave(osc1_wave, wrap(cycler - osc1_phase_offset * 0.25));
//
//spl0 += wave_l * osc1_gain;
//spl1 += wave_r * osc1_gain;
